{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### How do we know this helps? using annotations\n",
    "\n",
    "Recall that Cython compiles your code into C code for a python extension\n",
    "module. Depending on the information you provided (type annotations,\n",
    "`cdef`/`cpdef`) this will require more or less complicated C code. Cython\n",
    "provides tools to explore and optimize this process. To create an annotation of\n",
    "your file, on the command line run:\n",
    "\n",
    "~~~\n",
    "cython -a file_name.pyx\n",
    "~~~\n",
    "\n",
    "This generates your `.c` file, but also an `html` file with information about\n",
    "the line-by-line cost of the `pyx` file. The shade of yellow corresponds the\n",
    "number of lines of c that were generated, which highly correlates with the time\n",
    "of execution.\n",
    "\n",
    "Here's how this would be used in practice. Consider a simple case in which a\n",
    "helper function is used to calculate an increment, and this function is used by\n",
    "a more general function (`increment.pyx`):\n",
    "\n",
    "~~~\n",
    "def increment(int num, int offset):\n",
    "    return num + offset\n",
    "\n",
    "def increment_sequence(seq, offset):\n",
    "    result = []\n",
    "    for val in seq:\n",
    "        res = increment(val, offset)\n",
    "        result.append(res)\n",
    "    return result\n",
    "~~~\n",
    "\n",
    "Cythonize this file, and open the `increment.html` file, which annotates the\n",
    "cythonization process.\n",
    "\n",
    "Now, let's consider how much better we would do using `cdef` (`increment.pyx`):\n",
    "\n",
    "~~~\n",
    "cdef int fast_increment(int num, int offset):\n",
    "    return num + offset\n",
    "\n",
    "def fast_increment_sequence(seq, offset):\n",
    "    result = []\n",
    "    for val in seq:\n",
    "        res = fast_increment(val, offset)\n",
    "        result.append(res)\n",
    "    return result\n",
    "~~~\n",
    "\n",
    "Notice that in the second example, the lines corresponding to the first function\n",
    "are now completely white. The second example will also be much faster, because\n",
    "there is less Python-related overhead in that one. Cython can compile the\n",
    "fast_increment function without needing to do things like Python type-checking,\n",
    "etc.\n",
    "\n",
    "In this case, we may as well use `cpdef`:\n",
    "\n",
    "~~~\n",
    "cpdef int increment_either(int num, int offset):\n",
    "    return num + offset\n",
    "\n",
    "def fast_increment_sequence(seq, offset):\n",
    "    result = []\n",
    "    for val in seq:\n",
    "        res = increment_either(val, offset)\n",
    "        result.append(res)\n",
    "    return result\n",
    "~~~\n",
    "\n",
    "\n",
    "The function increment_either is only fast when called by\n",
    "fast_increment_sequence. However, you can now independently call it from Python\n",
    "(in which case, it will be slow).\n",
    "\n",
    "\n",
    "> ## Compling c extensions from c code.\n",
    "> This is useful if you want to use legacy C code. Consider the following toy\n",
    "> example. Let's say you had the following C code (in fact.h):\n",
    ">\n",
    ">     int fact(int n)\n",
    ">        {\n",
    ">        if (n<=1)\n",
    ">        return 1;\n",
    ">        return n * fact(n-1);\n",
    ">        }\n",
    ">\n",
    "> You could easily create a Python extension for this code by writing the Cython\n",
    "> file that contains the following (fact.pyx):\n",
    ">\n",
    ">     cdef extern from \"fact.h\":\n",
    ">         int _fact \"fact\"(int)\n",
    ">\n",
    ">     def fact(int n):\n",
    ">         return _fact(n)\n",
    ">     \n",
    ">\n",
    "> This is then compiled by typing:\n",
    ">\n",
    ">     cythonize fact.pyx  \n",
    ">\n",
    "> You can look at the fact.html file that gets generated with annotation to see\n",
    "> all the code you now *didn't* have to write.\n",
    "> Similar principles can be used with much more complex C code."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
